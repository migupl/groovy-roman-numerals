plugins {
    id 'groovy'
    id 'idea'
}

repositories {
    jcenter()
}

dependencies {
    implementation 'org.codehaus.groovy:groovy-all:2.5.4'

    testImplementation 'org.spockframework:spock-core:1.2-groovy-2.5'
}

version = 1.0

task calcRomanNumeral(type: JavaExec) {
    description 'Getting Roman Numeral'

    // Set main property to name of Groovy script class.
    main = 'CalcRomanNumeral'

    // Set classpath for running the Groovy script.
    classpath = sourceSets.main.runtimeClasspath

    // Pass command-line argument to script.
    if (project.hasProperty('number')) {
        args project.getProperty('number')
    }

    doFirst {
        ignoreExitValue = true
    }
}

task doJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Roman Numerals Subtractive Notation',
                'Implementation-Version': version,
                'Main-Class': 'com.roman.service.RomanNumeralsService'
    }

    baseName = 'roman-numerals-all'
    from {
        configurations.compile.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    } with jar
}

jar {
    exclude('CalcRomanNumeral.class')
}

defaultTasks 'calcRomanNumeral'
